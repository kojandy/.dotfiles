[[ $- == *i* ]] && stty -ixon

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

[ -z "$DOTFILES" ] && source "$HOME/.profile" || source "$HOME/.dotfiles/config/profile"
BASEDIR="${${DOTFILES:+$DOTFILES/config/zsh}:-${$(realpath ${funcsourcetrace[1]%:*})%/*}}"

[ ! -d "$HOME/.zgen" ] && git clone --depth 1 "https://github.com/tarjoilija/zgen.git" "$HOME/.zgen"
source "$HOME/.zgen/zgen.zsh"
if ! zgen saved; then
    zgen load romkatv/powerlevel10k powerlevel10k
    zgen load agkozak/zsh-z
    zgen load kojandy/zsh-bd
    zgen load zdharma-continuum/fast-syntax-highlighting
    [ ! -f "$HOME/.zgen/zdharma-continuum/fast-syntax-highlighting-master/current_theme.zsh" ] && \
        fast-theme clean

    zgen save
fi

KEYTIMEOUT=10
HISTSIZE=1000000
SAVEHIST=1000000
HISTFILE="$HOME/.zsh_history"

unsetopt share_history
setopt extended_history
setopt inc_append_history_time
setopt hist_find_no_dups
setopt auto_pushd
setopt pushd_ignore_dups
setopt pushd_silent

disable r

autoload -Uz up-line-or-beginning-search
autoload -Uz down-line-or-beginning-search
autoload -Uz edit-command-line
autoload -Uz surround
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
zle -N edit-command-line
zle -N delete-surround surround
zle -N add-surround surround
zle -N change-surround surround
bindkey -v
bindkey -rM viins '^D'
bindkey -M viins "^A" vi-beginning-of-line
bindkey -M viins "^E" vi-end-of-line
bindkey -M viins '^P' up-line-or-beginning-search
bindkey -M viins '^N' down-line-or-beginning-search
bindkey -M viins '^K' push-line
bindkey -M viins '^?' backward-delete-char
bindkey -rM vicmd ':'
bindkey -M vicmd v edit-command-line
bindkey -M vicmd cs change-surround
bindkey -M vicmd ds delete-surround
bindkey -M vicmd ys add-surround

autoload -Uz select-bracketed select-quoted
zle -N select-quoted
zle -N select-bracketed
for m in viopp visual; do
  for c in {a,i}${(s..)^:-\'\"\`\|,./:;=+@}; do
    bindkey -M $m -- $c select-quoted
  done
  for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
    bindkey -M $m -- $c select-bracketed
  done
done

zmodload zsh/complist
bindkey -M menuselect '^h' vi-backward-char
bindkey -M menuselect '^j' vi-down-line-or-history
bindkey -M menuselect '^k' vi-up-line-or-history
bindkey -M menuselect '^l' vi-forward-char
bindkey -M menuselect '^[' undo
zstyle ':completion:*' completer _extensions _complete _ignored _list _approximate
zstyle ':completion:*' menu select
zstyle ':completion::complete:*' menu select interactive
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-colors "${(@s.:.)LS_COLORS}"
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-prompt ''
zstyle ':completion:*' select-prompt ''
zstyle ':completion:*' file-sort change
zstyle ':completion:*:descriptions' format '%F{yellow}%B* %d%b%f'
zstyle ':completion:*:corrections' format '%F{red}%B* %d (errors: %e)%b%f'

source "$BASEDIR/aliases.zsh"
source "$BASEDIR/p10k.zsh"

[ -f "$HOME/.zshrc.local" ] && source "$HOME/.zshrc.local"
